plugins {
	id 'java'
	id 'idea'
	id "de.javaansehz.cluecumber-report-gradle-plugin" version "1.1.5"
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = 'de.javaansehz'
version = '0.0.1-SNAPSHOT'

def cucumberVersion = '4.8.0'
def junitVersion = '5.5.0'

repositories {
	mavenCentral()
}

dependencies {
	testImplementation "io.cucumber:cucumber-java8:${cucumberVersion}"
	testImplementation "io.cucumber:cucumber-junit:${cucumberVersion}"
	testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

configurations {
	cucumberRuntime {
		extendsFrom testImplementation
	}
}

test {
	useJUnitPlatform()
}

task cucumber() {
	dependsOn assemble, compileTestJava
	doLast {
		javaexec {
			main = "io.cucumber.core.cli.Main"
			classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
			args = ['--plugin', "json:${buildDir}/reports/cucumber-report.json",
					'--glue', 'de.javaansehz.cluecumber.example', "${projectDir}/features"]
		}
	}
}

cucumber.finalizedBy generateCluecumberReports

cluecumberReports {
	sourceJsonReportDirectory = "${buildDir}/reports"
	generatedHtmlReportDirectory = "${buildDir}/html-reports"
}
